<programa>         ::= "ESPETACULO_COMECA" <bloco> "ESPETACULO_TERMINA"
<bloco>            ::= "ABRE_CORTINA" <lista_comandos> "FECHA_CORTINA"
<lista_comandos>   ::= { <comando> }
<comando>          ::= <declaracao> | <atribuicao> | <condicional> | <loop> | <saida> | <bloco_ato> | <bloco_cena> | <entra_sai> | <improviso>
<declaracao>       ::= ("personagem" | "protagonista" | "coadjuvante" | "cenario") <identificador> [ "=" <expressao> ] "!!"
<atribuicao>       ::= <identificador> "=" <expressao> "!!"
<condicional>      ::= "se" "<<" <expressao_logica> ">>" <bloco_ato> [ "senao" <bloco_ato> ]
<loop>             ::= <loop_repetir> | <loop_caminhar>
<loop_repetir>     ::= "repetir" "<<" <expressao_logica> ">>" <bloco_ato>
<loop_caminhar>    ::= "caminhar" "<<" <atribuicao> <expressao_logica> "!!" <atribuicao> ">>" <bloco_ato>
<saida>            ::= ("fala" | "narrar") "<<" <expressao> ">>" "!!"
<bloco_ato>        ::= "ato" <bloco> | <bloco>
<bloco_cena>       ::= "cena" <identificador> <bloco>
<entra_sai>        ::= ("entra" | "sai") <identificador> "!!"
<improviso>        ::= "improviso" "<<" <expressao> ">>" "!!"
<expressao>        ::= <termo> { ("+" | "-") <termo> }
<termo>            ::= <fator> { ("*" | "/" ) <fator> }
<fator>            ::= <numero> | <decimal> | <identificador> | <string> | "<<" <expressao> ">>" | <improviso>
<expressao_logica> ::= <expressao_simples> { ("IGUAL_A" | "DIFERENTE_DE" | "MENOR_QUE" | "MAIOR_QUE" | "MENOR_IGUAL_A" | "MAIOR_IGUAL_A") <expressao_simples> }
<expressao_simples>::= <expressao>
<numero>           ::= [ "-" ] <digito> { <digito> }
<decimal>          ::= [ "-" ] <digito> { <digito> } "." <digito> { <digito> }
<digito>           ::= "0".."9"
<identificador>    ::= <letra> { <letra> | <digito> | "_" }
<letra>            ::= "a".."z"